// Generic Polyray input File
// Polyray input file: Alexander Enzmann

// Set up the camera
viewpoint {
   from <0,5,-7>
   at <0,0,0>
   up <0,1,0>
   angle 45
   resolution 256, 256
   }

background <0, 0, 0>
light <-10,20, -20>
include "../colors.inc"

define erode_exper (noise(10*P, 2) < 0.2 ? 1 :
		     (noise(10*P, 2) < 0.4 ?
			1 - 5 * (noise(10*P, 2) - 0.2) : 0))

define eroded_red
texture {
   special surface {
      normal N + 4 * (dnoise(10*P, 4) - <0.5, 0.5, 0.5>)
      ambient (1 - erode_exper) * red, 0.2
      diffuse (1 - erode_exper) * red, 0.5
      specular (1 - erode_exper) * white, 0.7
      microfacet Reitz 15
      transmission erode_exper, 1
      }
   }

object {
     object { sphere <0, 0, 0>, 2 shading_flags 55 eroded_red }
   & object { disc <0, 0, 0>, <0, 0, 1>, 3 }
   }

object {
   disc <0, -2, 0>, <0, 1, 0>, 10
   texture { checker matte_white, matte_black }
   }
