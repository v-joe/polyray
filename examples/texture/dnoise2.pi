// Generic Polyray input File
// Demonstrates a bumpy sphere by applying "dnoise" to the normal
// Polyray input file: Alexander Enzmann

// Set up the camera
viewpoint {
   from <0,0,-8>
   at <0,0,0>
   up <0,1,0>
   angle 45
   resolution 256, 256
   }

background pink
light 0.8*white, <-10, 10, -20>
light 0.8*white, < 10, 10, -20>
include "../colors.inc"

define wrinkly_red
texture {
   special shiny {
      color red
      normal N + 3 * (dnoise(2*P, <2,0.8,3>) - <0.5, 0.5, 0.5>)
      }
   }

object { sphere <0, 0, 0>, 2 wrinkly_red }
